plugins {
    id 'application'
}

group 'com.reign'
version '1.3.6'

repositories {
    mavenCentral()
    maven {url('https://m2.dv8tion.net/releases')}
    maven {url('https://jitpack.io')}
    maven {url("https://maven.lavalink.dev/releases")}
}

dependencies {
    testImplementation("junit:junit:4.13.2")

    // JDA
    implementation("net.dv8tion:JDA:5.1.1")

    // Lavaplayer
    implementation("dev.arbjerg:lavaplayer:2.1.2")
    implementation('com.github.Topis-Lavalink-Plugins:Topis-Source-Managers:2.0.7')

    // New Youtube source
    implementation("dev.lavalink.youtube:common:1.13.2")

    // VOSK
    implementation('net.java.dev.jna:jna:5.12.1')
    implementation('com.alphacephei:vosk:0.3.45')

    // slf4j logging
    implementation('org.slf4j:slf4j-api:2.0.5')
    implementation('ch.qos.logback:logback-classic:1.5.16')

    // Jackson
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.14.2')

    // AsciiTable
    implementation('de.vandermeer:asciitable:0.3.2')

    // Jsoup
    implementation('org.jsoup:jsoup:1.15.4')

    //MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.11.1')
}

tasks.withType(Tar.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Zip.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('generateVersion') {
    def outputFile = new File(projectDir, "src/main/resources/version.properties")
    def date = new Date().format("yyyy-MM-dd_hh-mm-ss")
    def commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()

    outputs.file outputFile
    doLast {
        outputFile.text = """
version=$project.version
timestamp=$date
commit=$commit
"""
    }
}
processResources.dependsOn(generateVersion)
build.dependsOn(generateVersion)

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "com.reign.kat.Main"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('runMain', JavaExec) {
    dependsOn 'jar'
    classpath = sourceSets.main.runtimeClasspath
}
