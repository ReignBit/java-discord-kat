plugins {
    id 'application'
}

group 'com.reign'
version '1.2.0'

repositories {
    mavenCentral()
    maven {url('https://m2.dv8tion.net/releases')}
    maven {url('https://jitpack.io')}
}

dependencies {
    testImplementation("junit:junit:4.13.2")

    // JDA
    implementation("net.dv8tion:JDA:5.0.0-beta.6")

    // Lavaplayer
    //implementation('com.github.walkyst:lavaplayer-fork:1.4.2')
    implementation("io.github.lavalink-devs:lavaplayer:727959e9f6")
    implementation('com.github.Topis-Lavalink-Plugins:Topis-Source-Managers:2.0.7')

    // VOSK
    implementation('net.java.dev.jna:jna:5.12.1')
    implementation('com.alphacephei:vosk:0.3.45')

    // slf4j logging
    implementation('org.slf4j:slf4j-api:2.0.5')
    implementation('ch.qos.logback:logback-classic:1.4.5')

    // Jackson
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.14.2')

    // AsciiTable
    implementation('de.vandermeer:asciitable:0.3.2')

    // Jsoup
    implementation('org.jsoup:jsoup:1.15.4')

    //MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.11.1')
}

tasks.withType(Tar.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Zip.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task generateVersion {
    def outputFile = new File(projectDir, "src/main/resources/version.properties")
    def date = new Date().format("yyyy-MM-dd_hh-mm-ss")
    def commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()

    outputs.file outputFile
    doLast {
        outputFile.text = """
version=$project.version
timestamp=$date
commit=$commit
"""
    }
}
processResources.dependsOn(generateVersion)
build.dependsOn(generateVersion)

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "com.reign.kat.Main"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task(runMain, dependsOn: 'jar', type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
}